								
model {										 		
for (i in 1 : nData) {						
for (j in 1 : 5) {        # NCOL(y) = 5
y[i,j] ~ dnorm(mu[i,j],psi[j])
}

mu[i,1] <- 1*xi[i,1]+ 0*xi[i,2]	 # xi[i,1]: Random Intercept, Initial Status
mu[i,2] <- 1*xi[i,1]+ 1*xi[i,2]         # xi[i,2]: Random Slope, Growth Rate    
mu[i,3] <- 1*xi[i,1]+ 2*xi[i,2]
mu[i,4] <- 1*xi[i,1]+ 3*xi[i,2]
mu[i,5] <- 1*xi[i,1]+ 4*xi[i,2]

xi[i,1] <- alpha1+ beta1*gender[i]+ eta[i,1] #Model for random intercept
xi[i,2] <- alpha2+ beta2*gender[i]+ eta[i,2] # Model for random slope

eta[i,1:2] ~ dmnorm(v[1:2],phi[1:2,1:2])
}

# Distributions and Priors

#Priors on Precisions
for(j in 1:5) {
psi[j] ~ dgamma(1, 0.001) # Error Precision 
sgm[j] <- 1/psi[j]        # Error variance
}

alpha1 ~ dnorm(0, 10^(-6)) #Mean of intercept    
alpha2 ~ dnorm(0, 10^(-6)) #Mean of slope 
beta1 ~ dnorm(0, 10^(-6))  #Intercept on gender
beta2 ~ dnorm(0, 10^(-6))  #Slope on gender    

phi[1:2,1:2] ~ dwish(R[1:2,1:2], 5)                   #phi is precision matrix of factors
Sigma[1,1] <- phi[2,2]/(phi[1,1]*phi[2,2]-phi[2,1]^2) #Sigma is covariance matrix of factors
Sigma[2,1] <- -phi[1,2]/(phi[1,1]*phi[2,2]-phi[2,1]^2)
Sigma[2,2] <- phi[1,1]/(phi[1,1]*phi[2,2]-phi[2,1]^2)
Sigma[1,2] <- Sigma[2,1]

}  

